apply plugin: 'com.android.library'
//kotlin
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//realm
apply plugin: 'realm-android'
//butterknife
apply plugin: 'com.jakewharton.butterknife'

//here is script to generate version code from version name
//which is set in gradle.properties as value of version var
//which must be in format major, minor, patch, where patch goes from 0 to 99
apply from: '../versioning.gradle'

apply from: '../marketSkusCreator.gradle'
apply from: '../apiKeysCreator.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    compileOptions.encoding = 'utf-8'

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }

    flavorDimensions "mode"

    def buildDateTime = project.hasProperty('devBuild') ? 100 : buildVersionCodeFor3Numbers()

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 26
        versionCode buildDateTime
        versionName version
        multiDexEnabled true

        manifestPlaceholders = [debuggable: true]
        buildConfigField "boolean", "TIMBER_ENABLE", "true"

        buildConfigField "String", "VK_API_VERSION", '"5.62"'

        buildConfigField('int', 'TIMEOUT_SECONDS_CONNECT', '60')
        buildConfigField('int', 'TIMEOUT_SECONDS_READ', '60')
        buildConfigField('int', 'TIMEOUT_SECONDS_WRITE', '60')

        buildConfigField('String', 'SCP_API_URL', '"https://scpfoundation.net/_api/wikidot_tags_search/"')

        //play market refuses not hardcoded strings as intent filter
        resValue('string', 'receiver_action_timer', '"ru.dante.scpfoundation.receivers.ReceiverTimer"')
        resValue('string', 'receiver_action_boot', '"android.intent.action.BOOT_COMPLETED"')
//        manifestPlaceholders = [receiver_action_timer: "ru.dante.scpfoundation.receivers.ReceiverTimer"]
//        manifestPlaceholders = [receiver_action_boot: "android.intent.action.BOOT_COMPLETED"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        prod {
            manifestPlaceholders = [myDebuggable: false]
            buildConfigField "boolean", "TIMBER_ENABLE", "false"

            dimension "mode"
        }

        dev {
            dimension "mode"

            manifestPlaceholders = [myDebuggable: true]

            //PNG optimizations are enabled by default, but are not needed for development builds.
            //Disable them to speed up your builds.
            aaptOptions.cruncherEnabled = false
            //only package russian translations, and xxhdpi resources
            resConfigs("ru", "xxhdpi")
        }

        libraryVariants.all { variant ->
            println("variantCore: " + variant.getName())

            fillArrays(variant, true)
            fillApiArrays(variant, true)
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    String supportLibraryVersion = '26.1.0'
    String googleLibraryVersion = '11.8.0'

    api fileTree(dir: 'libs', include: ['*.jar'])
    //kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //support libs
    api "com.android.support:appcompat-v7:$supportLibraryVersion"
    api "com.android.support:recyclerview-v7:$supportLibraryVersion"
    api "com.android.support:design:$supportLibraryVersion"
    api "com.android.support:cardview-v7:$supportLibraryVersion"
    //need this as gradle place warning on libs versions
    api "com.android.support:support-annotations:$supportLibraryVersion"
    api "com.android.support:customtabs:$supportLibraryVersion"
    api "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    api "com.android.support:support-v13:$supportLibraryVersion"
    api "com.android.support:support-v4:$supportLibraryVersion"
    api "com.android.support:support-compat:$supportLibraryVersion"
    //multidex to support more than 65K methods
    api 'com.android.support:multidex:1.0.2'
    //adapter delegates
    api 'com.hannesdorfmann:adapterdelegates3:3.0.1'
    //rx
    api 'io.reactivex:rxandroid:1.2.1'
    api 'io.reactivex:rxjava:1.3.4'
    api 'io.reactivex:rxkotlin:1.0.0'
    // Retrofit
    api('com.squareup.retrofit2:retrofit:2.3.0') { exclude module: 'okhttp' }
    api 'com.squareup.okhttp3:okhttp:3.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    api 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    //jsoup
    api 'org.jsoup:jsoup:1.10.3'
    //logs
    api 'com.jakewharton.timber:timber:4.6.0'
    //dagger2
    String daggerVersion = '2.13'
    api "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    //butterKnife
    String butterKnifeVersion = '8.8.1'
    api "com.jakewharton:butterknife:$butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    //image
    api 'com.github.bumptech.glide:glide:3.8.0'
    api 'com.github.chrisbanes:PhotoView:2.1.3'
    api 'de.hdodenhof:circleimageview:2.1.0'
    //mvp
    //WARNING!!!
    //Do not update to 3+ version, as there are problems with caching presenters that are not singleton
    //WARNING!!!
    api 'com.hannesdorfmann.mosby:mvp:2.0.1'
//    compile 'com.hannesdorfmann.mosby3:mvp-nullobject-presenter:3.0.4'
    //vk
    api 'com.vk:androidsdk:1.6.9'
    //facebook
    api 'com.facebook.android:facebook-android-sdk:4.27.0'
    //fonts
    api 'uk.co.chrisjenx:calligraphy:2.3.0'
    //firebase
    api "com.google.firebase:firebase-config:$googleLibraryVersion"
    api "com.google.firebase:firebase-crash:$googleLibraryVersion"
    api "com.google.firebase:firebase-core:$googleLibraryVersion"
    api "com.google.firebase:firebase-analytics:$googleLibraryVersion"
    api "com.google.firebase:firebase-invites:$googleLibraryVersion"
    api "com.google.firebase:firebase-ads:$googleLibraryVersion"
    api "com.google.firebase:firebase-database:$googleLibraryVersion"
    api "com.google.firebase:firebase-auth:$googleLibraryVersion"
    api "com.google.firebase:firebase-messaging:$googleLibraryVersion"
    //google login
    api "com.google.android.gms:play-services-auth:$googleLibraryVersion"
    //appodeal
    api "com.google.android.gms:play-services-ads:$googleLibraryVersion"
    api "com.google.android.gms:play-services-location:$googleLibraryVersion"
    //for Inmobi
    api 'com.squareup.picasso:picasso:2.5.2'
    api 'com.adcolony:sdk:3.2.1'
    api 'com.google.android.exoplayer:exoplayer:r2.5.4'
    //end of appodeal
    // AppMetrica
    //we add it here and delete jar from appodeal
    api 'com.yandex.android:mobmetricalib:2.77'
    //recyclerView fast scroller
    api 'xyz.danoz:recyclerviewfastscroller:0.1.3'
    //circular progress
    api 'com.github.jakob-grabner:Circle-Progress-View:v1.3'
    //tags layout
    api 'com.google.android:flexbox:0.3.0-alpha3'
    //range seekbar
    api 'com.crystal:crystalrangeseekbar:1.1.3'
    //leakcanary
    String leakcanaryVersion = '1.5.4'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    //time operations
    api 'joda-time:joda-time:2.9.9'

    //sub project modules
    api('ru.kuchanov.scp:downloads:1.0.24@aar') { transitive = true }
    api('ru.kuchanov.utils:rate:1.0.5@aar') { transitive = true }
}

//add it only in project build.gradle, not in lib
//apply plugin: 'com.google.gms.google-services'